apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

ext {
    publishedGroupId = 'ru.viewlifecycle'
    libraryVersion = '1.15-alpha'
}

android {
    compileSdkVersion 29

    defaultConfig {
        //noinspection MinSdkTooLow
        minSdkVersion 14
        targetSdkVersion 29
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    api("androidx.lifecycle:lifecycle-extensions:2.2.0") {
        transitive = true
    }
    api("androidx.fragment:fragment-ktx:1.2.5") {
        transitive = true
    }
}

project.afterEvaluate {
    publishing {
        publications {
            def release = android.libraryVariants.find { variant ->
                variant.name == 'release'
            }

            aar(MavenPublication) {
                pom.withXml {
                    def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')

                    // Iterate over the api dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.api.allDependencies.each {
                        // Ensure dependencies such as fileTree are not included.
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }

                artifact release.packageLibraryProvider.get()
                groupId = publishedGroupId
                artifactId = 'viewlifecycle'
                version libraryVersion
            }
        }
    }

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    bintray {
        user = properties.getProperty("bintray.user")
        key = properties.getProperty("bintray.apikey")
        publications = ['aar']
        pkg {
            version {
                name = libraryVersion
            }
            repo = 'maven'
            name = 'ViewLifecycle'
            desc = 'Android library for building fragmentless single activity apps based on lifecycle-aware Views.'
            websiteUrl = 'https://github.com/es-andreev/ViewLifecycle'
            vcsUrl = 'https://github.com/es-andreev/ViewLifecycle.git'
            licenses = ["Apache-2.0"]
            dryRun = false
            publish = true
            override = false
            publicDownloadNumbers = true
        }
    }
}
